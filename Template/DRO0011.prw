#INCLUDE 'TOTVS.CH'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao de variavel em objeto³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
 
#XTRANSLATE bSETGET(<uVar>) => { | u | If( PCount() == 0, <uVar>, <uVar> := u ) }

Static lUsaBaseTop := UsaBaseTop()

//ÚÄÄÄÄÄÄÄ¿
//³DEFINES³
//ÀÄÄÄÄÄÄÄÙ
//OPERADORES
#DEFINE OPE_AND 	If(lUsaBaseTop, " AND "," .AND. ")
#DEFINE OPE_OR  	If(lUsaBaseTop, " OR " ," .OR. ")
#DEFINE OPE_IGUAL	If(lUsaBaseTop, " = "  ," == ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Numero de caracteres dos operadores³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#DEFINE TAM_AND  	Len(OPE_AND)
#DEFINE TAM_OR  	Len(OPE_OR)
#DEFINE TAM_IGUAL	Len(OPE_IGUAL)

//ÚÄÄÄÄÄÄ¿
//³Campos³
//ÀÄÄÄÄÄÄÙ
#DEFINE	 CAMPO_B1_CODFAB  If(lUsaBaseTop,"SB1.B1_CODFAB" ,"B1_CODFAB") 
#DEFINE	 CAMPO_B1_LOJA    If(lUsaBaseTop,"SB1.B1_LOJA"   ,"B1_LOJA")
#DEFINE	 CAMPO_B1_CODAPRE If(lUsaBaseTop,"SB1.B1_CODAPRE","B1_CODAPRE")
#DEFINE	 CAMPO_B1_CODCOTL If(lUsaBaseTop,"SB1.B1_CODCOTL","B1_CODCOTL")
#DEFINE CAMPO_B1_CODPRIN If(lUsaBaseTop,"SB1.B1_CODPRIN","B1_CODPRIN")
#DEFINE CAMPO_B1_GENERIC If(lUsaBaseTop,"SB1.B1_GENERIC","B1_GENERIC")
#DEFINE CAMPO_B1_ALTERNA If(lUsaBaseTop,"SB1.B1_ALTERNA","B1_ALTERNA")

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³DROCENTRALºAutor  ³Vendas Clientes     º Data ³ 23/01/08    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Tela de filtro para a CENTRLA DE COMPRAS                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TEMPLATE - DROGARIA                                        º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Template Function DRO0011()
//³Objeto para a criacao da tela principal
Local oDlg

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros Devolvidos pela funcao  LocxGrid()         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³1. aRet	-	Array contendo 6 arrays como descrito abaixo:												³
//³				[1] = Array com o aHeader                                                                   ³
//³				[2] = Array contendo somente campos validos dos que foram passados                          ³
//³				[3] = Array contendo Titulo dos campos validos                                              ³
//³				[4] = Array contendo Tamanho dos campos validos												³
//³				[5] = Array co o conteudo das linhas (aCols)                                                ³
//³				[6] = Array contendo os Recnos referentes a cada linha                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Principio ativo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cSeekMHA    := '"' + xFilial("MHA") + '"'
Local cWhileMHA   := "!EOF() .AND. MHA_FILIAL == " + cSeekMHA
Local aPAtivo     := LocxGrid("MHA",cWhileMHA,,.T.,".F.",cSeekMHA,1,{"MHA_CODIGO","MHA_PATIVO"},)  
Local aTitPAtivo  := AClone(aPAtivo[3])
Local aContPAtivo := AClone(aPAtivo[5])
Local aTamCPAtivo := AClone(aPAtivo[4])
Local aHdrPAtivo  := AClone(aPAtivo[1]) 
Local oMarkPAtivo
Local lMarkPAtivo := .F.  
Local oPAtTWBrose   := NIL
Local oPAtTCheckBox := NIL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Apresentacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cSeekMHB  := '"' + xFilial("MHB") + '"'
Local cWhiMHBEL := "!EOF() .AND. MHB_FILIAL == " + cSeekMHB
Local aApre     := LocxGrid("MHB",cWhiMHBEL,,.T.,".F.",cSeekMHB,1,{"MHB_CODAPR","MHB_APRESE"},)  
Local aTitApre  := AClone(aApre[3])
Local aContApre := AClone(aApre[5])
Local aTamCApre := AClone(aApre[4])
Local aHdrApre  := AClone(aApre[1])  
Local oMarkApre
Local lMarkApre := .F.  
Local oAprTWBrose   := NIL
Local oAprTCheckBox := NIL      


//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Fabricante  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cSeekSA2    := '"' + xFilial("SA2") + '"'
Local cWhileSA2   := "!EOF() .And. A2_FILIAL == " + cSeekSA2
Local aFabric     := LocxGrid("SA2",cWhileSA2,'(A2_FABRICA=="S" .Or. A2_FABRICA=="1")',.T.,".F.",cSeekSA2,2,{"A2_COD","A2_LOJA","A2_NOME"},)  //Fabricantes
Local aTitFabric  := AClone(aFabric[3])
Local aContFabric := AClone(aFabric[5])
Local aTamCFabric := AClone(aFabric[4])
Local aHdrFabric  := AClone(aFabric[1])
Local oMarkFabric
Local lMarkFabric := .F. 
Local oFabTWBrose   := NIL
Local oFabTCheckBox := NIL      


//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cSeekLEO      := '"' + xFilial("LEO") + '"'
Local cWhiMHBEO     := "!EOF() .And. LEO_FILIAL == " + cSeekLEO
Local aControle     := LocxGrid("LEO",cWhiMHBEO,,.T.,".F.",cSeekLEO,2,{"LEO_CODCON","LEO_CONDES"},)  //Controles
Local aTitControle  := AClone(aControle[3])
Local aContControle := AClone(aControle[5])
Local aTamCControle := AClone(aControle[4])
Local aHdrControle  := AClone(aControle[1])
Local oMarkControle
Local lMarkControle := .F.  
Local oContTWBrose   := NIL
Local oContTCheckBox := NIL      


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Texto para os CheckBox³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cTexto1		:= "Marcar Todos"						//Texto para check BOX

Local aRadOpcGen 	:= {}	//Array contendo tipo para o objeto radio para medicamento generico		
Local oGenTRadMenu	:= NIL
Local oGenRMenu  	:= NIL

Local aRadOpcAlt 	:=	{}	//Array contendo tipo para o objeto radio para medicamento alternativo
Local oAltTRadMenu	:= NIL
Local oAltRMenu		:= NIL

Local oCompTela	    := DroCCompTela():CompTela()		//Localizado no fonte DRO005	
Local oParTGroup    := NIL

Local lRet		 	 := .F.  
Local cWhereFab  	 := ""	//Clausula WHERE criada a partir da selecao dos fabricantes
Local cWhereApre     := ""	//Clausula WHERE criada a partir da selecao das apresentacoes
Local cWhereControle := ""	//Clausula WHERE criada a partir da selecao dos controles
Local cWherePAtivo 	 := ""	//Clausula WHERE criada a partir da selecao dos princios ativo
Local cWhereGen 	 := "" 	//Clausula WHERE criada a partir da selecao do Medicamento Generico
Local cWhereAlt 	 := ""	//Clausula WHERE criada a partir da selecao do Medicamento Altenativo
Local cWhere	 	 := ""	//Clausula WHERE criada a partir da selecao dos registro	

Private oListPAtivo
Private oListApre
Private oListFabric
Private oListControle

Private	oOk     := LoadBitMap(GetResources(), "LBTIK")        	// Bitmap utilizado no Lisbox  (Marcado)
Private oNo     := LoadBitMap(GetResources(), "LBNO")			// Bitmap utilizado no Lisbox  (Desmarcado)
Private oNever  := LoadBitMap(GetResources(), "BR_VERMELHO")	// Bitmap utilizado no Lisbox  (Desabilitado)

/*verificamos se o sistema possui a licenca de
 Integracao Protheus x SIAC ou de Template de Drogaria*/
T_DROLCS()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tela da Central de Compras                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE "Central de Compras" FROM 1,1 TO 35,100

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Box - Fabricante  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oParTGroup := DROCParTGroup():ParTGroup( 005				, 005 , 089, 190,;
									     " Fabricantes     ", oDlg)	//Metodo CONSTRUTOR - DRO006.prw
oCompTela:CompTGroup(oParTGroup)


oFabTWBrose := DROCParTWBrowse():ParTWBrose(	014        , 010          , 170       , 60			,;
							   			    	oDlg       , 'oListFabric', aHdrFabric, aTitFabric	,;
										   		aTamCFabric, aContFabric ) //Metodo CONSTRUTOR - DRO007.prw
oListFabric := oCompTela:CompTWBrose(oFabTWBrose)									     											


oFabTCheckBox := DROCParTCheckBox():ParTCheckBox( 076	  , 135 , oDlg, @lMarkFabric,;
												   cTexto1, 50) //Metodo CONSTRUTOR - DRO008.prw

oMarkFabric := oCompTela:CompTCheckBox(oFabTCheckBox)	

oMarkFabric:bLClicked := {|| aEval( oFabTWBrose:aConteudo , { |x,y| oFabTWBrose:aConteudo[y,1] := If(oFabTCheckBox:lMarcado ,1,-1)}) }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Box - Apresentacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oParTGroup := DROCParTGroup():ParTGroup( 092				, 005 , 176, 190,;
									     " Apresentação    ", oDlg) //Metodo CONSTRUTOR - DRO006.prw
oCompTela:CompTGroup(oParTGroup)									     


oAprTWBrose := DROCParTWBrowse():ParTWBrose(	101        , 010          , 170     , 60		,;
							   			    oDlg       , 'oListApre'  , aHdrApre, aTitApre	,;
											aTamCApre  , aContApre ) //Metodo CONSTRUTOR - DRO007.prw 
oListApre := oCompTela:CompTWBrose(oAprTWBrose)									     											


oAprTCheckBox := DROCParTCheckBox():ParTCheckBox( 163	  , 135 , oDlg, @lMarkApre,;
												   cTexto1, 50) //Metodo CONSTRUTOR - DRO008.prw

oMarkApre := oCompTela:CompTCheckBox(oAprTCheckBox)	
oMarkApre:bLClicked := {|| aEval( oAprTWBrose:aConteudo , { |x,y| oAprTWBrose:aConteudo[y,1] := If(oAprTCheckBox:lMarcado ,1,-1)}) }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Box - Controle    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oParTGroup := DROCParTGroup():ParTGroup( 005				, 200 , 089, 385,;
									     " Controles       ", oDlg) //Metodo CONSTRUTOR - DRO006.prw
oCompTela:CompTGroup(oParTGroup)									     


oContTWBrose := DROCParTWBrowse():ParTWBrose(	014        , 205          , 170     , 60		,;
							   			    oDlg       , 'oListControle'  , aHdrControle, aTitControle	,;
											aTamCControle  , aContControle ) //Metodo CONSTRUTOR - DRO007.prw  
oListControle := oCompTela:CompTWBrose(oContTWBrose)									     											


oContTCheckBox := DROCParTCheckBox():ParTCheckBox( 76	  , 330 , oDlg, @lMarkControle,;
												   cTexto1, 50) //Metodo CONSTRUTOR - DRO008.prw

oMarkApre := oCompTela:CompTCheckBox(oContTCheckBox)	
oMarkApre:bLClicked := {|| aEval( oContTWBrose:aConteudo , { |x,y| oContTWBrose:aConteudo[y,1] := If(oContTCheckBox:lMarcado ,1,-1)}) }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Box - Principio Ativo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oParTGroup := DROCParTGroup():ParTGroup( 092				, 200 , 176, 385,;
									     " Princípio Ativo ", oDlg) //Metodo CONSTRUTOR - DRO006.prw
oCompTela:CompTGroup(oParTGroup)									     


oPAtTWBrose := DROCParTWBrowse():ParTWBrose(	101        , 205          , 170     , 60		,;
							   			    oDlg       , 'oListPAtivo'  , aHdrPAtivo, aTitPAtivo	,;
											aTamCPAtivo  , aContPAtivo ) //Metodo CONSTRUTOR - DRO007.prw   
oListPAtivo := oCompTela:CompTWBrose(oPAtTWBrose)									     											


oPAtTCheckBox := DROCParTCheckBox():ParTCheckBox( 163	  , 330 , oDlg, lMarkPAtivo,;
												   cTexto1, 50) //Metodo CONSTRUTOR - DRO008.prw  

oMarkPAtivo := oCompTela:CompTCheckBox(oPAtTCheckBox)	
oMarkPAtivo:bLClicked := {|| aEval( oPAtTWBrose:aConteudo , { |x,y| oPAtTWBrose:aConteudo[y,1] := If(oPAtTCheckBox:lMarcado ,1,-1)}) }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Radio - Medicamento Generico³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oParTGroup := DROCParTGroup():ParTGroup( 180					 , 005 , 220, 190,;
								   		  " Medicamento Genérico", oDlg) //Metodo CONSTRUTOR - DRO006.prw  
oCompTela:CompTGroup(oParTGroup)

aRadOpcGen 	 := {"Sim", "Não", "Todos"}
oGenTRadMenu := DROCParTRadMenu():ParTRadMenu(188    , 010, oDlg, aRadOpcGen,;
							   			      1) //Metodo CONSTRUTOR - DRO009.prw  
						   			      
oCompTela:CompTRadMenu(oGenTRadMenu)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Radio - Medicamento Alternativo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ  
oParTGroup := DROCParTGroup():ParTGroup( 180						, 200 , 220, 385,;
								   		  " Medicamento Alternativo", oDlg) //Metodo CONSTRUTOR - DRO006.prw  
oCompTela:CompTGroup(oParTGroup)


aRadOpcAlt := {"Sim", "Não", "Todos"}
oAltTRadMenu := DROCParTRadMenu():ParTRadMenu(188    , 205, oDlg, aRadOpcAlt,;
                                              1) //Metodo CONSTRUTOR - DRO009.prw  
oCompTela:CompTRadMenu(oAltTRadMenu)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Botoes de Confirma / Cancela³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE SBUTTON FROM 233,325 TYPE 1 ACTION (lRet := .T., oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 233,355 TYPE 2 ACTION (lRet := .F., oDlg:End()) ENABLE OF oDlg


ACTIVATE MSDIALOG oDlg CENTER

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄ¿
	//³Fabricante³
	//ÀÄÄÄÄÄÄÄÄÄÄÙ
	cWhereFab := CompWhere(oFabTWBrose:aConteudo, 1)	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apresentacao³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWhereApre := CompWhere(oAprTWBrose:aConteudo, 2)	
	
	//ÚÄÄÄÄÄÄÄÄÄ¿
	//³Controles³
	//ÀÄÄÄÄÄÄÄÄÄÙ
	cWhereControle := CompWhere(oContTWBrose:aConteudo, 3)	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Principio Ativo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWherePAtivo := CompWhere(oPAtTWBrose:aConteudo, 4) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Medicamento Generico³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWhereGen := CompWhere(NIL, 5, oGenTRadMenu:nOpcoes) 		   
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Medicamento Alternativo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cWhereAlt := CompWhere(NIL, 6, oAltTRadMenu:nOpcoes) 		   
		
	cWhere := If(cWhereFab 		<> "()", cWhereFab		+ OPE_AND,"")+;
	          If(cWhereApre 	<> "()", cWhereApre	  	+ OPE_AND,"")+;
	          If(cWhereControle <> "()", cWhereControle	+ OPE_AND,"")+;
	          If(cWherePAtivo 	<> "()", cWherePAtivo	+ OPE_AND,"")+;
	          If(cWhereGen 		<> "()", cWhereGen		+ OPE_AND,"")+;
	          If(cWhereAlt 		<> "()", cWhereAlt		+ OPE_AND,"")

	If cWhere <> ""
		cWhere := Left(cWhere, Len(cWhere) - TAM_AND)
	Else
		cWhere := "()"
	Endif
Else
	cWhere := "()"
Endif 

Return (cWhere)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CompWhere ºAutor  ³Vendas Clientes     º Data ³ 21/01/08    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Criacao da clausula WHERE baseado nas opcoes marcadas na   º±±
±±º          ³ tela de parametros da Central de Compras                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpA1 - Array contendo informacoes de filtro               º±±
±±º          ³ ExpN2 - Tipo de filtro a ser trabalhado                    º±±
±±º          ³         nTipo = 1 (Fabricante)                             º±±
±±º          ³         nTipo = 2 (Apresentacao)                           º±±
±±º          ³         nTipo = 3 (Controles)                              º±±
±±º          ³         nTipo = 4 (Principio Ativo)                        º±±
±±º          ³         nTipo = 5 (Medicamento Generico)                   º±±
±±º          ³         nTipo = 6 (Medicamento Alternativo)                º±±
±±º          ³ ExpN3 - Opcao escolhida para Medicamento Generico e        º±±
±±º          ³         Medicamento Alternativo                            º±±
±±º          ³         nOpcRadio = 1 (Filtrar Medicamentos Generico/Alter º±±
±±º          ³         nOpcRadio = 2 (Nao Filtrar Medicamento Genric/Alterº±±
±±º          ³         nOpcRadio = 3 Desconsiderar filtro para este campo º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ ExpC1 - Clausula WHERE montadacoes de filtro               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TEMPLATE - DROGARIA                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function CompWhere(aArray, nTipo, nOpcRadio)
Local cWhere 		:= ""		// Clausula WHERE a ser montada de acordo com o preenchimento da tela Central de Compras 
Local aContAux  	:= {}		// Array auxiliar para armazenar os registros marcados
Local nTamAContAux	:= 0		// Tamanho do array aContAux	
Local nFor      	:= 0		// Controle de loop	

DEFAULT aArray 		:= {} 
DEFAULT nTipo  		:= 0  
DEFAULT nOpcRadio  	:= 0  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a funcao esta sendo chamada a partir de     ³
//³Medicamento Generico (5) ou Medicamento Alternartivo (6)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nTipo <> 5 .AND. nTipo <> 6)
	AEval(aArray, {|x| If(x[1]==1,AtuArray(nTipo, @aContAux, x[2], x[3]),NIL)})
	nTamAContAux := Len(aContAux)
    cWhere := "("
	If nTamAContAux > 0
		For nFor := 1 to nTamAContAux
			If nTipo == 1		//Fabricante
				cWhere += CAMPO_B1_CODFAB + OPE_IGUAL + "'" + aContAux[nFor][1] + "'" + OPE_AND
				cWhere += CAMPO_B1_LOJA   + OPE_IGUAL + "'" + aContAux[nFor][2] + "'" 
				If(nFor <> nTamAContAux) 
					cWhere += OPE_OR
				Endif	
			ElseIf nTipo == 2	//Apresentacao
				cWhere += CAMPO_B1_CODAPRE + OPE_IGUAL + "'" + aContAux[nFor][1] + "'"
				If(nFor <> nTamAContAux) 
					cWhere += OPE_OR
				Endif					
			ElseIf nTipo == 3	//Controles
				cWhere += CAMPO_B1_CODCOTL + OPE_IGUAL + "'" + aContAux[nFor][1] + "'"
				If(nFor <> nTamAContAux) 
					cWhere += OPE_OR
				Endif					
			ElseIf nTipo == 4	//Principio Ativo
				cWhere += CAMPO_B1_CODPRIN +  OPE_IGUAL + "'" + aContAux[nFor][1] + "'"
				If(nFor <> nTamAContAux) 
					cWhere += OPE_OR
				Endif					
			Endif
		Next nFor
	Endif	
	cWhere += ")" 	
Else
	cWhere := "("
	If nTipo == 5	//Medicamento Generico
		If nOpcRadio == 1 	//Opcao SIM selecionada  
			cWhere += CAMPO_B1_GENERIC + OPE_IGUAL + "'S'" 
		ElseIf nOpcRadio == 2 //Opcao NAO selecionada
			cWhere += CAMPO_B1_GENERIC + OPE_IGUAL + "'N'" 
		Endif
	ElseIf nTipo == 6	//Medicamento Alternativo
		If nOpcRadio == 1 	//Opcao SIM selecionada  
			cWhere += CAMPO_B1_ALTERNA + OPE_IGUAL + "'1'" 
		ElseIf nOpcRadio == 2 //Opcao NAO selecionada
			cWhere += CAMPO_B1_ALTERNA + OPE_IGUAL + "'2'" 
		Endif	
	Endif
	cWhere += ")"	
EndIf

Return (cWhere)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AtuArray  ºAutor  ³Vendas Clientes     º Data ³ 21/01/08    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Preenche o array com os registros selecionados             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpN1 - Tipo de filtro a ser trabalhado                    º±±
±±º          ³         nTipo = 1 (Fabricante)                             º±±
±±º          ³         nTipo = 2 (Apresentacao)                           º±±
±±º          ³         nTipo = 3 (Controles)                              º±±
±±º          ³         nTipo = 4 (Principio Ativo)                        º±±
±±º          ³ ExpA2 - Array que ira' armazenar os registros selecionados º±±
±±º          ³ ExpC3 - Codigo                                             º±±
±±º          ³ ExpC4 - Loja (somente p/ filtro dos Fabricantes)           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TEMPLATE - DROGARIA                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtuArray(nTipo, aContAux, cCod, cLoj )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³nTipo = 1 (Fabricante), significa que devera armazenar³
//³CODIGO DO FABRICANTE + LOJA                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTipo == 1 
	aAdd(aContAux,{cCod,cLoj})
Else
	aAdd(aContAux,{cCod})
Endif

Return (.T.)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³UsaBaseTopºAutor  ³Vendas Clientes     º Data ³ 21/01/08    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica qual a base de dados e' utilizada, TOP ou DBF      º±± 
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL1  - Usa ou nao a base TOP                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TEMPLATE - DROGARIA (Central de Compras)                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function UsaBaseTop()
Local lUsaTOP := .F.		//Verifica se utiliza TOP

#IFDEF TOP 
	If TcSrvType() <> "AS/400"
		lUsaTOP := .T.
	EndIf
#ENDIF	

Return (lUsaTOP)